plugins {
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.2"
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id "org.jetbrains.kotlin.kapt" version "1.7.20"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group "gg.scala.cgs"
    version "1.4.2"

    repositories {
        mavenCentral()

        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }

        maven { url "https://repo.glaremasters.me/repository/concuncan/" }

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://oss.sonatype.org/content/repositories/central" }

        maven {
            name "scala"
            url "${property("artifactory_contextUrl")}/gradle-release"

            credentials {
                username property("artifactory_user")
                password property("artifactory_password")
            }
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "gg.scala.lemon:bukkit:1.6.3"
        compileOnly "gg.scala.spigot:server:1.1.0"

        compileOnly "gg.scala.store:spigot:0.1.8"
        compileOnly "gg.scala.store:shared:0.1.8"

        compileOnly "gg.scala.cloudsync:spigot:1.0.1"

        compileOnly "com.grinderwolf:slimeworldmanager-api:2.2.1"

        compileOnly "gg.scala.commons:bukkit:3.4.5"
        kapt "gg.scala.commons:bukkit:3.4.5"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "17"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "cgs-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }

        repositories {
            maven {
                name "scala"
                url "${property("artifactory_contextUrl")}/gradle-release"

                credentials {
                    username property("artifactory_user")
                    password property("artifactory_password")
                }
            }
        }
    }

    tasks.build.dependsOn(
        shadowJar,
        publishShadowPublicationToScalaRepository
    )
}

artifactory {
    this.contextUrl = property("artifactory_contextUrl")

    publish {
        repository {
            repoKey = property("artifactory_release_local")
            username = property("artifactory_user")
            password = property("artifactory_password")
            maven = true
        }
    }
}
