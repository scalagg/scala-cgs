plugins {
    id 'maven-publish'
    id 'net.kyori.blossom' version '1.2.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
}

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }

    return stdout.toString()
            .trim().toString()
} as String

def gitBranch = { ->
    def stdout = new ByteArrayOutputStream()

    exec {
        commandLine 'git', 'branch', '--show-current'
        standardOutput = stdout
    }

    return stdout.toString()
            .trim().toString()
} as String

allprojects {
    group 'gg.scala.cgs'
    version gitHash

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url "https://repo.lucko.me" }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "net.kyori.blossom"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "gg.scala.lemon:bukkit:1.0.0"
        compileOnly "gg.scala:sspigot-server:1.0-SNAPSHOT"

        compileOnly fileTree("library")
        compileOnly "me.lucko:helper:5.6.8"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "cgs-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }
    }

    blossom {
        replaceToken '@version', "origin/$gitBranch/$gitHash"
    }

    tasks.build.dependsOn(shadowJar, publishing)
}
