plugins {
    id "maven-publish"
    id "com.jfrog.artifactory" version "4.28.2"
    id "org.jetbrains.kotlin.jvm" version "1.6.21"
    id "org.jetbrains.kotlin.kapt" version "1.6.21"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

allprojects {
    group 'gg.scala.cgs'
    version "1.0.6"

    repositories {
        mavenCentral()

        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }

        maven {
            url "${artifactory_contextUrl}/gradle-release"
            credentials {
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }
}

subprojects {
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "com.github.johnrengelman.shadow"

    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        compileOnly "gg.scala.lemon:bukkit:1.2.1"
        compileOnly 'gg.scala.spigot:sspigot-server:1.0.7'

        compileOnly "net.evilblock.cubed:bukkit:1.3.7"

        compileOnly "gg.scala.store:spigot:0.1.3"
        compileOnly "gg.scala.store:shared:0.1.3"

        compileOnly 'gg.scala.cloudsync:spigot:1.0.1'

        compileOnly "me.lucko:helper:5.6.10.1"
        kapt "me.lucko:helper:5.6.10.1"
    }

    compileKotlin {
        kotlinOptions.javaParameters = true
        kotlinOptions.jvmTarget = "1.8"
    }

    shadowJar {
        archiveClassifier.set("")

        exclude "**/*.kotlin_metadata"
        exclude "**/*.kotlin_builtins"
        exclude "META-INF/"

        archiveFileName = "cgs-${project.name}.jar"
    }

    publishing {
        publications {
            shadow(MavenPublication) { publication ->
                project.shadow.component(publication)
            }
        }

        repositories {
            maven {
                name "scala"
                url "${artifactory_contextUrl}/${artifactory_release}"
                credentials {
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
            }
        }
    }

    tasks.build.dependsOn(
        shadowJar,
        publishShadowPublicationToScalaRepository
    )
}

artifactory {
    this.contextUrl = "${artifactory_contextUrl}"

    publish {
        repository {
            repoKey = "${artifactory_release_local}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
